buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
//tag::applygroovyplugin[]
apply plugin: 'groovy'
//end::applygroovyplugin[]
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.asciidoctor.convert'

//tag::repositories[]
repositories {
    jcenter()
}
//end::repositories[]

//tag::versiongroup[]
version projectVersion
group "com.objectcomputing"
//end::versiongroup[]
//tag::dependencies[]
dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    compile "org.gebish:geb-core:1.1.1"
    compile "org.seleniumhq.selenium:selenium-firefox-driver:2.53.1"
    compile "org.seleniumhq.selenium:selenium-support:2.53.1"
    compile "net.sourceforge.htmlunit:htmlunit:2.18"
    compile "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
}
//end::dependencies[]
//tag::test[]
test {
    systemProperties System.properties
}
//end::test[]

apply from: "${rootProject.projectDir}/gradle/testVerbose.gradle"

asciidoctor {
    backends 'html5'
    attributes \
        'build-gradle': file('build.gradle'),
        'sourcedir': project.sourceSets.main.java.srcDirs[0],
        'imagesdir': './images',
        'source-highlighter': 'coderay'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    from sourceSets.main.allGroovy
}

task javadocJar(type:Jar) {
    classifier = 'javadoc'
    from groovydoc.outputs
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER") ?: project.hasProperty("bintrayUser") ? project.bintrayUser : ''
    key = System.getenv("BINTRAY_KEY") ?: project.hasProperty("bintrayKey") ? project.bintrayKey : ''
    publications = ['maven']
    publish = true
    pkg {
        name = "oci-training-scraper"
        desc = "Scraper library for OCI Training"
        repo = project.hasProperty('repo') ? project.repo : 'plugins'
        userOrg = project.hasProperty('userOrg') ? project.userOrg : 'grails'
        websiteUrl = "https://github.com/sdelamo/${rootProject.name}"
        issueTrackerUrl = "https://github.com/sdelamo/${rootProject.name}/issues"
        vcsUrl = "git@github.com:github.com/sdelamo/${rootProject.name}.git"
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}